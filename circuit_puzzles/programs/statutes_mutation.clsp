
(mod (STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_DF PAST_CUMULATIVE_INTEREST_DF
      CAT_MOD_HASH CRT_TAIL_HASH PRICE_UPDATE_COUNTER
      mutation_index mutation_value crt_inner_puzzle_hash)

  (include *standard-cl-23.1*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defconst CRT_ANN_PREFIX 'G')
  (defconst CUSTOM_ANNOUNCEMENTS_MUTATION_INDEX -1)

  (defun filter-ann-conditions (
    (@ ann_conditions
      (
        (@ condition
          (condition_code . condition_body)
        )
        . rest
      )
    ) filtered_announcements)
    (if ann_conditions
      (if
        (any
          (= condition_code ASSERT_COIN_ANNOUNCEMENT)
          (= condition_code CREATE_COIN_ANNOUNCEMENT)
        )
        (filter-ann-conditions rest (c condition filtered_announcements))
        (if (any (= condition_code SEND_MESSAGE) (= condition_code RECEIVE_MESSAGE))
          (if
            (= (substr (f (r condition_body)) 0 ONE) STATUTES_ANN_PREFIX)
            ; bad message, error
            (x)
            (filter-ann-conditions rest (c condition filtered_announcements))
          )
          (x)
        )
      )
      filtered_announcements
    )
  )

  (defun-inline get-crt-puzzle-hash (CAT_MOD_HASH CRT_TAIL_HASH INNER_PUZZLE_HASH)
    (curry_hashes CAT_MOD_HASH
      (sha256 ONE CAT_MOD_HASH)
      (sha256 ONE CRT_TAIL_HASH)
      INNER_PUZZLE_HASH
    )
  )

  (list
    ; we're updating if indexes match and it's not announcements
    (if (= mutation_index CUSTOM_ANNOUNCEMENTS_MUTATION_INDEX)
      ; this is a vote for announcements
      STATUTES
      ; update statutes
      (mutate-list mutation_index mutation_value STATUTES 0)
    )
    ; prices staying the same
    PRICE_INFO
    ; don't change the rates
    PAST_CUMULATIVE_STABILITY_FEE_DF
    PAST_CUMULATIVE_INTEREST_DF
    PRICE_UPDATE_COUNTER

    ;  updating statutes, assert permission
    (assert
      (size_b32 crt_inner_puzzle_hash)
      ; -1 is for announcements, rest are for statutes
      (> mutation_index -2)
      (> 37 mutation_index) ; there are 37 statutes, no more
      (if (= mutation_index CUSTOM_ANNOUNCEMENTS_MUTATION_INDEX)
        ; vote for announcements only, so this should just be a list
        (l mutation_value)
        ; should be  a list with 5 elements if mutating statutes
        (not (r (r (r (r (r mutation_value))))))
      )
      (li
        ; get confirmation from a CRT voting coin that mutation was applied
        (list RECEIVE_MESSAGE 0x12
          (concat STATUTES_ANN_PREFIX STATUTES_MUTATION_PREFIX (sha256tree (c mutation_index mutation_value)))
          (get-crt-puzzle-hash CAT_MOD_HASH CRT_TAIL_HASH crt_inner_puzzle_hash)
        )
        (list ASSERT_HEIGHT_RELATIVE ONE)
        &rest
        (if (= mutation_index CUSTOM_ANNOUNCEMENTS_MUTATION_INDEX)
          ; filter passes conditions for customer announcements, otherwise ignore
          (filter-ann-conditions (f mutation_value) ())
          ()
        )
      )
    )
  )
)