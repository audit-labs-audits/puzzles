(mod
  (
    (BILL MOD_HASH INNER_PUZZLE_HASH CAT_MOD_HASH CRT_TAIL_HASH STATUTES_STRUCT statutes_puzzle_hash)
    (
      _ ; new coin amount
      target_bill_hash
      relative_time_spent
      target_coin_id
      lineage_proof ; -> (parent_parent_id parent_bill_hash parent_amount) if BILL, else ()
    )
  )

  (include *standard-cl-23.1*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (assign
    (
      (veto_seconds . enact_seconds)
      statute_index
      statute_value
      threshold_amount_to_propose
      new_veto_seconds
      delay_seconds
      max_delta
    ) (if BILL BILL (list (c 0 0) () () () () () ()))
    (assert
      (size_b32 target_bill_hash)
      (size_b32 target_coin_id)
      (list
        (if BILL
          (list
            ; we allow votes that are in proposal mode to veto other votes, we just need
            ; to update the enact seconds to whatever is passed with relative_time_spent
            ; it's up to the user to make this time as close to current time as possible
            (c
              (if (> relative_time_spent veto_seconds)
                1
                (- veto_seconds relative_time_spent)
              )
              (if (> relative_time_spent enact_seconds)
                1
                (- enact_seconds relative_time_spent)
              )
            )
            statute_index
            statute_value
            threshold_amount_to_propose
            new_veto_seconds
            delay_seconds
            max_delta
          )
          ()
        )
        (list 
          (if BILL
            ; we are vetoing from a proposal coin. must have a parent that was a governance coin to prevent
            ; eve governance coin having curried bill and being enact-able in next spend without ever having
            ; had to perform a propose spend.
            (list ASSERT_MY_PARENT_ID
              (calculate-coin-id
                (f lineage_proof) ; parent parent id
                (curry_hashes CAT_MOD_HASH
                  (sha256tree CAT_MOD_HASH)
                  (sha256tree CRT_TAIL_HASH)
                  (curry_hashes MOD_HASH
                    (sha256tree MOD_HASH)
                    (sha256tree CAT_MOD_HASH)
                    (sha256tree CRT_TAIL_HASH)
                    (sha256tree STATUTES_STRUCT)
                    (sha256tree INNER_PUZZLE_HASH)
                    (f (r lineage_proof)) ; parent bill hash
                  )
                )
                (f (r (r lineage_proof))) ; parent amount
              )
            )
            (list REMARK)
          )
          (list ASSERT_MY_PUZZLE_HASH
            (curry_hashes CAT_MOD_HASH
              (sha256 ONE CAT_MOD_HASH)
              (sha256 ONE CRT_TAIL_HASH)
              (curry_hashes MOD_HASH
                (sha256 ONE MOD_HASH)
                (sha256 ONE CAT_MOD_HASH)
                (sha256 ONE CRT_TAIL_HASH)
                (sha256tree STATUTES_STRUCT)
                (sha256 ONE INNER_PUZZLE_HASH)
                (sha256tree BILL)
              )
            )
          )
          ; this timestamp must be in the past, so it's up to the voter to push it as close to present as possible
          (list ASSERT_SECONDS_RELATIVE relative_time_spent)
          (list SEND_MESSAGE 0x3f (concat PROTOCOL_PREFIX (sha256tree target_bill_hash)) target_coin_id)
        )
      )
    )
  )
)
