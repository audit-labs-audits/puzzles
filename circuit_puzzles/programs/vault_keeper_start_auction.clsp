(mod
  (
    (@ VAULT_STATE
      (
       COLLATERAL PRINCIPAL AUCTION_STATE INNER_PUZZLE_HASH
       STATUTES_STRUCT DISCOUNTED_PRINCIPAL TRANSFERRED_FEES statutes_puzzle_hash
      )
    )
    (@ args
      (
        auction_start_time
        step_time_interval
        step_price_decrease_factor
        initiator_incentive_flat_fee
        initiator_incentive_relative_fee_bps
        liquidation_ratio
        price_info
        starting_price_factor
        initiator_puzzle_hash
        auction_timeout
        statutes_cumulative_stability_fee_df
        current_stability_fee_df
        liquidation_penalty_bps
        minimum_debt_amount
      )
    )
  )

  (include *standard-cl-23.1*)
  (include vault.clib)
  (include statutes_utils.clib)
  (include condition_codes.clib)
  (include utils.clib)

  ; NOTE: as in tradfi loan agreements, if the lender seizes collateral, there’s no further interest being
  ; charged on the borrower anymore (whereas it typically would if there’s a late payment only
  ; that does not allow the lender to seize collateral)
  (assign
    cumulative_stability_fee_df (calculate-cumulative-discount-factor
      statutes_cumulative_stability_fee_df
      current_stability_fee_df
      ; don't allow anyone to exploit our processing interval buffer to get tx into mempool and borrow from the future
      (+ auction_start_time (* 3 MAX_TX_BLOCK_TIME))
      (r price_info)
    )
    undiscounted_principal (* MINUS_ONE (/ (* MINUS_ONE DISCOUNTED_PRINCIPAL cumulative_stability_fee_df) PRECISION))
    min_collateral_amount (get-min-collateral-amount
      undiscounted_principal
      liquidation_ratio
      (f price_info)
    )
    ; start_price -> last_price * starting_price_factor
    start_price (/ (* (f price_info) starting_price_factor) PRECISION_BPS) 
    prev_auction_start_time (if AUCTION_STATE (f AUCTION_STATE) auction_start_time)
    initiator_incentive_relative (/ (* undiscounted_principal initiator_incentive_relative_fee_bps) PRECISION_BPS)
    initiator_incentive_balance (+ initiator_incentive_flat_fee initiator_incentive_relative)
    total_fees (calculate-total-fees
      undiscounted_principal
      PRINCIPAL
      liquidation_penalty_bps
    )
    byc_to_treasury_balance (- total_fees initiator_incentive_balance TRANSFERRED_FEES)
    byc_to_burn_balance (+ PRINCIPAL TRANSFERRED_FEES)
    auction_state (list
      ; current auction start time, we need this to figure out if auction has timed out so we can restart it
      auction_start_time
      start_price
      step_price_decrease_factor
      step_time_interval
      initiator_puzzle_hash
      initiator_incentive_balance
      auction_timeout
      minimum_debt_amount
      byc_to_treasury_balance
      byc_to_burn_balance
    )
    (list
      (list
        0 ; TRANSFERRED_FEES
        COLLATERAL
        0 ; PRINCIPAL
        auction_state
        INNER_PUZZLE_HASH
        0 ; DISCOUNTED_PRINCIPAL
      )
      (assert
        ; is previous auction still running by checking if auction_start_time hasn't timed out
        (any
          (> (- auction_start_time prev_auction_start_time) auction_timeout)
          (not AUCTION_STATE)
        )
        ; we must be below min collateral ratio to start an auction
        (> min_collateral_amount COLLATERAL)
        ; there should be collateral to auction off, o/w it's bad debt
        (> COLLATERAL 0)
        (list
          (list ASSERT_SECONDS_ABSOLUTE (- auction_start_time MAX_TX_BLOCK_TIME))
          (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ auction_start_time MAX_TX_BLOCK_TIME))
          (assert-price-info statutes_puzzle_hash price_info)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_LIQUIDATION_RATIO_PCT liquidation_ratio)
          (assert-statute statutes_puzzle_hash STATUTE_CUMULATIVE_STABILITY_FEE_DF statutes_cumulative_stability_fee_df)
          (assert-statute statutes_puzzle_hash STATUTE_STABILITY_FEE_DF current_stability_fee_df)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_STARTING_PRICE_FACTOR starting_price_factor)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_PRICE_DECREASE_BPS step_price_decrease_factor)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_PRICE_TTL step_time_interval)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_INITIATOR_INCENTIVE_BPS initiator_incentive_relative_fee_bps)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_INITIATOR_INCENTIVE_FLAT initiator_incentive_flat_fee)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_TTL auction_timeout)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_LIQUIDATION_PENALTY_BPS liquidation_penalty_bps)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_MINIMUM_DEBT minimum_debt_amount)
        )
      )
    )
  )
)