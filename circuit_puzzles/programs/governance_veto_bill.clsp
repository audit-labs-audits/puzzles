(mod
  (
    (BILL MOD_HASH INNER_PUZZLE_HASH CAT_MOD_HASH CRT_TAIL_HASH STATUTES_STRUCT
     statutes_puzzle_hash)
    (
      amount
      parent_veto_id
      veto_amount
      veto_inner_puzzle_hash
      veto_bill_hash
    )
  )

  (include *standard-cl-23.1*)
  (include crt_utils.clib)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defconst ANN_VETOED 'x')

  (assign
    (
      (veto_seconds . enact_seconds)
      new_statute_index
      new_statute_value
      new_threshold_amount_to_propose
      new_veto_seconds
      new_delay_seconds
      new_max_delta
    ) BILL
    (assert
      ; can only veto if in proposal mode (BILL is not empty)
      BILL
      (> veto_amount amount)
      (> veto_seconds 0)

      (list
        ()
        (list
          (if (> veto_seconds ONE)
            (list ASSERT_BEFORE_SECONDS_RELATIVE veto_seconds)
            ; noop
            (list ASSERT_SECONDS_RELATIVE veto_seconds)
          )
          (list CREATE_COIN_ANNOUNCEMENT (concat PROTOCOL_PREFIX (sha256tree ANN_VETOED)))
          ; assert announcement from another CRT coin with higher amount than this one
          (list RECEIVE_MESSAGE 0x3f
            (concat PROTOCOL_PREFIX (sha256tree (sha256tree BILL)))
            (calculate-coin-id
              parent_veto_id
              (calculate-full-gov-puzzle-hash
                MOD_HASH
                CAT_MOD_HASH
                CRT_TAIL_HASH
                STATUTES_STRUCT
                veto_inner_puzzle_hash
                veto_bill_hash
              )
              veto_amount
            )
          )
        )
      )
    )
  )
)